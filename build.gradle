apply plugin: 'groovy'
apply plugin: 'cobertura'
apply plugin: 'maven'
apply plugin: 'idea'

sourceSets {
    main {
        groovy {
            srcDirs = ['src']
        }
    }

    test {
        groovy {
            srcDirs = ['test']
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:2.2.4"
    }
}

sourceCompatibility = 1.7
version = "0.1.${System.env.BUILD_NUMBER}"

repositories {
    mavenCentral()
    maven {
        url "https://nixilla.net/nexus/content/groups/public/"
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.1'
    compile 'ganja:common:0.1.1'
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.groovy
}

artifacts {
    archives sourcesJar
}

cobertura {
    coverageDirs << file("/build/classes/main")
    coverageSourceDirs = [ sourceSets.main.groovy.srcDirs ]
    coverageFormats = ['xml', 'html']
    coverageExcludes = [ '.*example.*' ]
}

if (!hasProperty('sonatypeUsername')) {
    ext.sonatypeUsername = ''
}
if (!hasProperty('sonatypePassword')) {
    ext.sonatypePassword = ''
}
if (!hasProperty('sonatypeUrl')) {
    ext.sonatypeUrl = ''
}

uploadArchives {

    group 'build'
    description = "Does a maven deploy of archives artifacts"

    repositories {
        mavenDeployer {
            repository(url: "${sonatypeUrl}/releases/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.version = version
            pom.artifactId = "routing"
            pom.groupId = "ganja.component"
            pom.project {
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}
